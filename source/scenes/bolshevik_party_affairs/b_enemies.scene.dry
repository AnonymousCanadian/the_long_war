title: Choosing Our Enemies
new-page: true
view-if: b_enemies_timer <= 0 and month < 9
on-arrival: b_enemies_timer = 6; week_actions += 1
is-card: true
card-image: img/soviet_leaders.jpg
tags: b_party

= Choosing Our Enemies

In our media and campaign materials, who should we consider our primary enemies?

- @bourgeois
- @counterrevolutionaries
- @mensheviks
- @imperialists
- @easy_discard
- @cancel_advisor_action

@counterrevolutionaries
title: Counterrevolutionary forces, Black Hundreds, [? if kornilov_affair_seen : Kornilovites, ?] and the like.
on-arrival: workers_m += 3*(1-b_dissent); b_b_relation += 3*(1-b_dissent); internationalist_dissent -= 2; defensist_dissent -= 2;

Attacking counterrevolutionary forces is a longstanding theme in Social Democratic propaganda.

@bourgeois
title: The Kadets and other bourgeois parties.
on-arrival: b_k_relation -= 8; internationalist_dissent -= 4; internationalist_strength += 5*(1-b_dissent); social_patriot_dissent += 5; defensist_dissent += 4; b_b_relation += 5*(1-b_dissent); soviet_cooperation -= 0.05;

Attacking the Kadets is certainly popular among the Bolshevik base.

@mensheviks
title: Mensheviks and SRs, the bourgeois socialists.
on-arrival: m_b_relation -= 8; b_sr_relation -= 8; workers_m -= (5+b_workers_org)*(1-b_dissent); workers_sr -= (5+b_workers_org)*(1-b_dissent); soldiers_m -= (5+b_soldiers_org)*(1-b_dissent); soldiers_sr -= (5+b_soldiers_org)*(1-b_dissent);

Attacking the bourgeois socialists reduces their standing among the working class and the soldiers.

@imperialists
title: The imperialist powers - Britain and France primarily.
on-arrival: b_k_relation -= 10; war_weariness += 5; prov_gov_legitimacy -= 5; leninist_strength += 8;

Attacking the "Allies" raises doubts about the Provisional Government's policy, and reduces its legitimacy.
