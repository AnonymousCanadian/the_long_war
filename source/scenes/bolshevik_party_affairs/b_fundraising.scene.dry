title: Fundraising
new-page: true
view-if: fundraising_timer <= 0
on-arrival: fundraising_timer = 6; week_actions += 1
frequency: 300
is-card: true
card-image: img/poster_2.jpg
tags: b_party

= Fundraising

For better or for worse, the party cannot exist without money.

- @maintain: Maintain our current dues.
- @reduce: Reduce dues.
- @increase: Increase dues.

@maintain
subtitle: [+ b_dues +] resources gained.
on-arrival: b_resources += b_dues;

We maintain our current dues.

@reduce
subtitle: [+ b_dues - 1 +] resources gained.
choose-if: b_dues > 1
on-arrival: b_dues -= 1; b_resources += b_dues; leninist_dissent -= 10; center_dissent -= 8; moderate_dissent -= 8;

Our current members appreciate this, but no one decides to join a party because of the low dues.

@increase
subtitle: [+ b_dues + 1 +] resources gained. [? if hunger >= 15 : Given the present economic situation, this may be painful. ?][? if b_dues >= 4 : Dues are already rather high; increasing them might reduce our membership. ?]
on-arrival: b_dues += 1; b_resources += b_dues; workers_bolshevik -= 3; underclass_bolshevik -= 3; workers_bolshevik -= 10 if hunger >= 15; underclass_bolshevik -= 10 if hunger >= 15; workers_bolshevik -= 3*b_dues if b_dues >= 5; underclass_bolshevik -= 3*b_dues if b_dues >= 5
view-if: b_dues <= 4

Our members grumble at this, and some of the less committed members leave. But now, we have access to additional resources.

[? if hunger >= 15: Given the present levels of poverty and hunger, raising dues may be a bad idea. ?]
