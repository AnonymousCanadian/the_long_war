title: Soviet Organizations
new-page: true
view-if: organization_soviet_timer = 0 and ((player_party = "b" and b_in_exco) or (player_party = "m" and m_in_exco) or (player_party = "sr" and sr_in_exco) or (player_party = "k" and kadet_in_exco))
on-arrival: week_actions += 1; organization_soviet_timer += 3
tags: soviet
is-card: true
card-image: img/soviet_2.jpg

= Soviet Organizations

The Petrograd Soviet is a largely ad-hoc council; perhaps we should build up organs for longer-term governance.

- @org_1
- @org
- @administrative
- @improve_production
- @root: No action is needed.
- @easy_discard
- @cancel_advisor_action

@org_1
title: Create an economic coordination infrastructure for the workers' councils.
subtitle: -1 resource
choose-if: (player_party = "m" and m_resources >= 1) or (player_party = "sr" and sr_resources >= 1) or (player_party = "b" and b_resources >= 1) 
on-arrival: soviet_organization += 1; soviet_efficacy += 0.05; soviet_economic_coordination += 1
view-if: workers_councils >= 1 or factory_takeovers >= 1

We have started a council for coordinating the distribution of resources across enterprises in Petrograd and beyond. In the long run, this should allow us to improve our industrial production and reduce working hours.

@org
title: Build up economic coordination between factories and enterprises.
subtitle: -1 resource
choose-if: (player_party = "m" and m_resources >= 1) or (player_party = "sr" and sr_resources >= 1) or (player_party = "b" and b_resources >= 1)
view-if: soviet_organization > 0
on-arrival: soviet_organization += 1; soviet_efficacy += 0.05; m_resources -= 1 if player_party = "m"; sr_resources -= 1 if player_party = "sr"; b_resources -= 1 if player_party = "b"; soviet_economic_coordination += 1

We are continuing to build up economic coordination infrastructure.

@administrative
title: Build up a Soviet administrative apparatus.
subtitle: -2 resources
choose-if: (player_party = "m" and m_resources >= 2) or (player_party = "sr" and sr_resources >= 2) or (player_party = "b" and b_resources >= 2)
on-arrival: soviet_organization += 1; soviet_efficacy += 0.1; m_resources -= 2 if player_party = "m"; sr_resources -= 2 if player_party = "sr"; b_resources -= 2 if player_party = "b"

Some of our detractors are arguing that we are bureaucratizing the Soviet, but we are creating the organs that will allow the Soviet to act as an actual government.

@improve_production
title: Use our Soviet infrastructure to improve production.
choose-if: soviet_efficacy >= 0.7
view-if: workers_councils >= 1 or factory_takeovers >= 1
unavailable-subtitle: We do not have enough administrative capacity in the Soviet.
on-arrival: industry *= (1+0.02*soviet_efficacy);

We are attempting to use the Soviet's administrative apparatus and the connections forged among the workers' councils to find ways to improve production. It is a difficult task.

