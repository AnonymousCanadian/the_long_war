title: War Policy
choose-if: m_war_policy_timer = 0
unavailable-subtitle: It is too early to change this policy again.
on-arrival: m_war_policy_timer = 6;

# maybe this should be a subset of questions of ideology

= War Policy

The war is the greatest question in politics at this moment. What should be our policy regarding the war?

- @rd
- @rd_2
- @i_1
- @i_2
- @i_3
- @sp_1
- @sp_2
- @sp_3

@rd
title: Continue a policy of revolutionary defensism.
subtitle: Support an international peace without annexations or indemnities, but fight to defend the revolution in the meantime. [? if m_war_policy = 1: This is our current position. ?]
view-if: m_war_policy = 1
on-arrival: defensist_strength += 5; defensist_dissent -= 4; internationalist_dissent += 5

We are continuing to support a policy of revolutionary defensism.

@rd_2
title: Adopt a policy of revolutionary defensism.
subtitle: Support an international peace without annexations or indemnities, but fight to defend the revolution in the meantime.
unavailable-subtitle: There is not enough support for this position within the party.
view-if: m_war_policy != 1
choose-if: defensist_strength >= internationalist_strength and defensist_strength >= social_patriot_strength
on-arrival: m_war_policy = 1; defensist_strength += 10; internationalist_dissent += 20 if m_war_policy > 1; m_leadership = 0;

We have adopted a revolutionary defensist position, calling on continued support of the war effort while negotiating for a general peace.


@i_1
title: Adopt an internationalist position.
subtitle: Support an immediate end to the war, even a separate peace if necessary.
unavailable-subtitle: There is not enough support for this position within the party.
view-if: m_war_policy != 2
choose-if: internationalist_strength >= defensist_strength + social_patriot_strength
on-arrival: m_war_policy = 2; internationalist_strength += 5; internationalist_dissent -= 5; defensist_dissent += 20; social_patriot_dissent += 30; m_leadership = 1; m_sr_relation -= 8 if sr_leadership = 0; m_k_relation -= 10;

We have adopted an internationalist position, calling for an immediate end to the war.

[? if defensist_dissent > 55 : The Defensists are deeply upset at this decision, and thus are splitting from the party. ?]

- @m_defensist_split

# TODO: go to defensist split

@i_2
title: Increase support for an internationalist position.
subtitle: Support an immediate end to the war, even a separate peace if necessary.
view-if: m_war_policy != 2 and internationalist_strength < defensist_strength + social_patriot_strength
on-arrival: internationalist_strength += 6; internationalist_dissent -= 4; defensist_dissent += 10; social_patriot_dissent += 10; m_sr_relation -= 2 if sr_leadership = 0; m_k_relation -= 3;

We are building up support for internationalism within the party, so that we may eventually adopt this position.

@i_3
title: Continue to support an internationalist position.
subtitle: Support an immediate end to the war, even a separate peace if necessary.
view-if: m_war_policy = 2
on-arrival: internationalist_strength += 4; internationalist_dissent -= 3; social_patriot_dissent += 4; defensist_dissent += 4

We are continuing to support an internationalist position.

@sp_1
title: Adopt a social patriot position.
subtitle: We must fight the war until we win.
unavailable-subtitle: There is not enough support for this position within the party.
view-if: m_war_policy > 0 
choose-if: social_patriot_strength >= internationalist_strength
on-arrival: m_war_policy = 0; social_patriot_strength += 10; internationalist_dissent += 30

We have adopted a social patriot position, calling for the victory of the Allies.

@sp_2
title: Support a social patriot position.
subtitle: We must fight the war until we win.
view-if: m_war_policy < 0 and social_patriot_strength < internationalist_strength
on-arrival: social_patriot_strength += 4; social_patriot_dissent -= 5; internationalist_dissent += 5; defensist_dissent += 4

We are building up support for social patriotism within the party, so that we may eventually adopt this position.

@sp_3
title: Continue to support a social patriot position.
subtitle: We must fight the war until we win.
view-if: m_war_policy = 0
on-arrival: social_patriot_strength += 4; social_patriot_dissent -= 5; internationalist_dissent += 5; defensist_dissent += 4

We are continuing to support a social patriot position.
