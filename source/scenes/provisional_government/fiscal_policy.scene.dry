title: Fiscal Policy
new-page: true
is-card: true
on-arrival: fiscal_policy_timer += 4; week_actions += 1
view-if: fiscal_policy_timer == 0 and ((player_party = "k" and k_in_gov and (prime_minister = "Lvov" or finance_minister_party = "Kadet" or finance_minister_party = "Independent" or finance_minister_party = "Progressive" or finance_minister_party = "Octobrist" or finance_minister_party = "Trudovik" or finance_minister_party = "Popular Socialist")) or (player_party = "sr" and sr_in_gov and (prime_minister = "Kerensky" or finance_minister_party = "SR" or finance_minister_party = "Trudovik" or finance_minister_party = "Popular Socialist")) or (player_party = "m" and m_in_gov and (prime_minister = "Tsereteli" or finance_minister_party = "Menshevik")) or (player_party = "b" and b_in_gov and finance_minister_party = "Bolshevik"))
card-image: img/ruble.jpg
tags: prov_gov


= Fiscal Policy

For the past few years, the Tsarist regime has been printing money and taking loans from the western allies to finance the war, leading to spiraling inflation. We must bring our fiscal affairs back into balance somehow. Taxation, bonds, and controlling the budget are potential levers.

Current tax levels:

Income taxes: [+ income_taxes : taxation +]

Business taxes: [+ business_taxes : taxation +]

Indirect taxes: [+ indirect_taxes : taxation +]

- @raise_taxes: Raise income taxes.
- @cut_taxes
- @raise_profit_taxes: Raise excess profit taxes on businesses.
#- @raise_indirect_taxes: Raise indirect taxes.
- @loans: Take out war bonds.
- @foreign_loans: Take out more loans from foreign powers.
- @discipline: Engage in budget discipline.
- @printers: Turn on the money printers!
- @root: Do not change the tax system.
- @easy_discard
- @cancel_advisor_action

@raise_taxes
subtitle: +1 budget
unavailable-subtitle: Taxes are already too high.
choose-if: income_taxes < 5
on-arrival: budget += 1; middle_none += 5; capital_flight_progress += 1; capital_flight_progress += 1 if income_taxes >= 2; workers_m -= 3 if player_party = "m"; workers_sr -= 3 if player_party = "sr"; workers_k -= 3 if player_party = "k"; middle_m -= 5 if player_party = "m"; middle_sr -= 5 if player_party = "sr"; middle_k -= 5 if player_party = "k" and income_taxes >= 2; income_taxes += 1; monthly_inflation -= 0.2;

Russia has historically had a weak tax base, relying primarily on indirect taxes. Only the middle class and some of the better-paid workers, less than a quarter of the working population, pay income taxes.

Our tax increases hurts the livelihoods of workers and the middle class, and cuts into business confidence.

@cut_taxes
title: Cut income taxes.
subtitle: -1 budget
view-if: budget >= 1
on-arrival: budget -= 1; capital_flight_progress -= 1; workers_m += 3*(1-m_dissent) if player_party = "m"; workers_sr += 3*(1-sr_dissent) if player_party = "sr"; workers_k += (1-k_dissent)*3 if player_party = "k"; middle_m += 5*(1-m_dissent) if player_party = "m"; middle_sr += 5*(1-sr_dissent) if player_party = "sr"; middle_k += 5*(1-k_dissent) if player_party = "k" and income_taxes >= 2; income_taxes -= 1; monthly_inflation += 0.2;

We have cut income taxes for the workers and middle class.

@raise_profit_taxes
subtitle: +1 budget
unavailable-subtitle: Taxes are already too high.
choose-if: business_taxes < 5
on-arrival: budget += 1; middle_none += 5; capital_flight_progress += 1; capital_flight_progress += 1 if business_taxes >= 1; elite_m -= 5 if player_party = "m"; elite_sr -= 5 if player_party = "sr"; elite_k -= 5 if player_party = "k"; middle_m -= 5 if player_party = "m"; middle_sr -= 5 if player_party = "sr"; middle_k -= 5 if player_party = "k" and business_taxes >= 1; coup_progress += 1 if business_taxes >= 2; business_taxes += 1; monthly_inflation -= 0.2;

We have raised excess profit taxes on businesses. This cuts into business confidence.

@cut_profit_taxes
view-if: budget >= 1

@raise_indirect_taxes
subtitle: +1 budget
on-arrival: budget += 1; workers_none += 5; workers_m -= 6 if player_party = "m"; workers_sr -= 6 if player_party = "sr"; workers_k -= 6 if player_party = "k"; capital_flight_progress += 1 if indirect_taxes >= 1; indirect_taxes += 1; monthly_inflation -= 0.2;

We have raised indirect taxes - fees for government services, taxes on internal trade, and so on. This hurts the livelihoods of the workers.

@cut_indirect_taxes
view-if: budget >= 1

@loans
subtitle: +2 budget
unavailable-subtitle: People cannot afford to buy any more bonds. 
choose-if: bonding_capacity >= 2 
on-arrival: budget += 2; bonding_capacity -= 2; monthly_inflation -= 0.2;

[? if bonding_capacity >= 2 and liberty_loan_seen = 0 : We have issued the "Liberty Loans", a new set of war bonds. ?]
[? if bonding_capacity < 2 or liberty_loan_seen > 0 : We have issued a new set of bonds. Unfortunately, the people might not be able to purchase more bonds beyond this point. ?]

@foreign_loans
subtitle: +2 budget
unavailable-subtitle: Our relation with the West is not good enough.
choose-if: west_relation >= 60
on-arrival: west_relation -= 10; west_loans += 2; foreign_debt += 2; budget += 2

We have taken out a new set of foreign loans.

@discipline
subtitle: +2 budget - cut government salaries and benefits.
unavailable-subtitle: There is nothing left to cut.
choose-if: spending > -5
on-arrival: budget += 2; spending -= 2; middle_none += 5; workers_none += 5; soldiers_none += 5;  workers_m -= 3 if player_party = "m"; workers_sr -= 3 if player_party = "sr"; workers_k -= 3 if player_party = "k"; middle_m -= 3 if player_party = "m"; middle_sr -= 3 if player_party = "sr"; middle_k -= 3 if player_party = "k"; soldiers_m -= 5 if player_party = "m"; soldiers_sr -= 5 if player_party = "sr"; soldiers_k -= 5 if player_party = "k"; train_capacity *= 0.99; morale -= 0.04; benefits -= 1; monthly_inflation -= 0.4; prov_gov_efficacy -= 0.06;

Cutting the budget hurts the workers and middle class, and also reduces the morales of the soldiers and railroad workers, two of the largest groups of government employees. This makes them less effective at their jobs.

@printers
subtitle: +2 budget
on-arrival: monthly_inflation += 0.4; budget += 2; workers_none += 5; middle_none += 5; soldiers_none += 5;

We have turned on the money printers. This is bad for our government's legitimacy, and will contribute to inflation.

