title: Industrial Policy
new-page: true
is-card: true
on-arrival: industrial_policy_timer += 4; week_actions += 1;
view-if: industrial_policy_timer == 0 and ((player_party = "k" and k_in_prov_gov and (trade_minister_party = "Kadet" or trade_minister_party = "Independent" or trade_minister_party = "Progressive" or trade_minister_party = "Octobrist" or trade_minister_party = "Trudovik" or trade_minister_party = "Popular Socialist")) or (player_party = "sr" and sr_in_prov_gov and (trade_minister_party = "SR" or trade_minister_party = "Trudovik" or trade_minister_party = "Popular Socialist")) or (player_party = "m" and m_in_prov_gov and trade_minister_party = "Menshevik"))
card-image: img/Putilov.jpg
tags: prov_gov

= Industrial Policy

During the war, the Tsarist government essentially became the coordinator of all industrial production in Russia. The Provisional Government now must do the same. We must balance the needs of the war with the needs of the people.

Current production allocation:

Military: [+ military_industry +]%

Civilian goods: [+ civilian_industry +]%

Agricultural equipment: [+ agricultural_industry +]%

Transportation equipment: [+ transportation_industry +]%

Capital goods: [+ capital_industry +]%

- @ind_menu: Change industrial production.
- @improve
- @demilitarization: Return the industries to civilian footing.
- @root: No production changes are necessary.
- @easy_discard
- @cancel_advisor_action


@ind_menu

Military: [+ military_industry +]%

Civilian goods: [+ civilian_industry +]%

Agricultural equipment: [+ agricultural_industry +]%

Transportation equipment: [+ transportation_industry +]%

Capital goods: [+ capital_industry +]%

**Unused production capacity: [+ unused_industry +]**

- @reduce_military
- @reduce_civilian
- @reduce_agricultural
- @reduce_transportation
- @reduce_capital
- @increase_military
- @increase_civilian
- @increase_agricultural
- @increase_transportation
- @increase_capital
- @root: No more changes are necessary.



@reduce_military
title: Reduce military production.
subtitle: During the war, the bulk of the industry has been devoted to making guns and ammunition for the army.
unavailable-subtitle: [? if industrial_changes >= industrial_changes_limit : We can only retool so much production at once. ?] [? if military_industry < 5 : We cannot reduce anymore. ?]
choose-if: industrial_changes < industrial_changes_limit and military_industry >= 5
on-arrival: military_industry -= 5; unused_industry += 5; industrial_changes += 5
go-to: ind_menu

@reduce_civilian
title: Reduce civilian production.
subtitle: Since the war began, production of consumer goods has shrunk greatly.
unavailable-subtitle: [? if industrial_changes >= industrial_changes_limit : We can only retool so much production at once. ?] [? if civilian_industry < 5 : We cannot reduce anymore. ?]
choose-if: industrial_changes < industrial_changes_limit and civilian_industry >= 5
on-arrival: civilian_industry -= 5; unused_industry += 5; industrial_changes += 5
go-to: ind_menu

@reduce_agricultural
title: Reduce agricultural production.
subtitle: Since the war began, production of agricultural equipment has shrunk greatly.
unavailable-subtitle: [? if industrial_changes >= industrial_changes_limit : We can only retool so much production at once. ?] [? if agricultural_industry < 5 : We cannot reduce anymore. ?]
choose-if: industrial_changes < industrial_changes_limit and agricultural_industry >= 5
on-arrival: agricultural_industry -= 5; unused_industry += 5; industrial_changes += 5
go-to: ind_menu

@reduce_transportation
title: Reduce transportation production.
subtitle: Train cars, locomotives, and rail tracks are in great demand.
unavailable-subtitle: [? if industrial_changes >= industrial_changes_limit : We can only retool so much production at once. ?] [? if transportation_industry < 5 : We cannot reduce anymore. ?]
choose-if: industrial_changes < industrial_changes_limit and transportation_industry >= 5
on-arrival: transportation_industry -= 5; unused_industry += 5; industrial_changes += 5
go-to: ind_menu

@reduce_capital
title: Reduce capital production.
subtitle: Production of factory equipment and tools necessary to expand production.
unavailable-subtitle: [? if industrial_changes >= industrial_changes_limit : We can only retool so much production at once. ?] [? if capital_industry < 5 : We cannot reduce anymore. ?]
choose-if: industrial_changes < industrial_changes_limit and capital_industry >= 5
on-arrival: capital_industry -= 5; unused_industry += 5; industrial_changes += 5
go-to: ind_menu


@increase_military
title: Increase military production.
subtitle: During the war, the bulk of the industry has been devoted to making guns and ammunition for the army.
unavailable-subtitle: [? if unused_industry < 5 : There is not enough unused industrial capacity. ?] [? if military_industry >= 80 : We cannot increase anymore. ?]
choose-if: unused_industry >= 5 and military_industry < 80
on-arrival: military_industry += 5; unused_industry -= 5;
go-to: ind_menu

@increase_civilian
title: Increase civilian production.
subtitle: Since the war began, production of consumer goods has shrunk greatly.
unavailable-subtitle: [? if unused_industry < 5 : There is not enough unused industrial capacity. ?] [? if civilian_industry >= 80 : We cannot increase anymore. ?]
choose-if: unused_industry >= 5 and civilian_industry < 80
on-arrival: civilian_industry += 5; unused_industry -= 5
go-to: ind_menu

@increase_agricultural
title: Increase agricultural production.
subtitle: Since the war began, production of agricultural equipment has shrunk greatly.
unavailable-subtitle: [? if unused_industry < 5 : There is not enough unused industrial capacity. ?] [? if agricultural_industry >= 80 : We cannot increase anymore. ?]
choose-if: unused_industry >= 5 and agricultural_industry < 80
on-arrival: agricultural_industry += 5; unused_industry -= 5
go-to: ind_menu

@increase_transportation
title: Increase transportation production.
subtitle: Train cars, locomotives, and rail tracks are in great demand.
unavailable-subtitle: [? if unused_industry < 5 : There is not enough unused industrial capacity. ?] [? if transportation_industry >= 80 : We cannot increase anymore. ?]
choose-if: unused_industry >= 5 and transportation_industry < 80
on-arrival: transportation_industry += 5; unused_industry -= 5
go-to: ind_menu

@increase_capital
title: Increase capital production.
subtitle: Production of factory equipment and tools necessary to expand production.
unavailable-subtitle: [? if unused_industry < 5 : There is not enough unused industrial capacity. ?] [? if capital_industry >= 80 : We cannot increase anymore. ?]
choose-if: unused_industry >= 5 and capital_industry < 80
on-arrival: capital_industry += 5; unused_industry -= 5
go-to: ind_menu

@improve
title: Improve industrial production with the aid of the Soviet.
subtitle: Success depends on our level of cooperation with the Soviet and its efficacy.
choose-if: soviet_cooperation >= 0.7 and soviet_efficacy >= 0.7 and improve_production_count <= capital_industry
unavailable-subtitle: [? if soviet_cooperation <= 0.7: The Provisional Government does not have enough cooperation with the Soviet. ?][? if soviet_efficacy < 0.7 : The Soviet does not have the administrative capacity to help. ?][? if improve_production_count >= capital_industry : There is not much room to improve without more capital production. ?]
on-arrival: improve_production_count += 1; industry += 10*soviet_efficacy*soviet_cooperation*prov_gov_efficacy;

With the cooperation of the Petrograd Soviet, We have embarked on a project to organize industrial production and improve the efficiency of our industry. [? if prov_gov_efficacy < 0.6 : The inefficiency of our own government is a hindrance here. ?]


@demilitarization
title: Return the industries to civilian footing.
subtitle: This is a major undertaking - we will shift all of our military industries to various civilian uses.
view-if: (is_armistice or is_peace) and not is_demilitarized
on-arrival: {!
Q.is_demilitarized = 1;
var mil = Q.military_industry;
// distribute half to other stuff...
Q.civilian_industry += Math.round(mil*3/4);
Q.agricultural_industry += Math.round(mil*1/8);
Q.capital_industry += Math.round(mil*1/8);
Q.transportation_industry += Math.round(mil*1/16);
var total = Q.civilian_industry + Q.agricultural_industry + Q.capital_industry + Q.transportation_industry;
if (total < 100) {
    Q.civilian_industry += 100 - total;
} else if (total > 100) {
    Q.civilian_industry -= total - 100;
}
Q.military_industry = 0;
Q.unemployed += 4;
!}

We are rapidly demilitarizing our industries, shifting everything to various forms of civilian production. There are some inefficiencies involved, and many workers are laid off in the process.
