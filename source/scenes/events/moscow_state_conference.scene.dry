title: Conference of Public Figures
new-page: True
subtitle: How can the Provisional Government regain its legitimacy?
view-if: month >= 8 and prov_gov_legitimacy <= 50
tags: event
max-visits: 1

= Conference of Public Figures

In Moscow, a conference is held to stabilize the Provisional Government by bringing together important individuals from both the left and the right, the proletariat and the bourgeoisie. The goal is to bridge the gap between the upper classes and the lower-class socialists. 

Immediately, the conference is wracked by dissension as the Moscow trade unions launch a strike.

- @attack
- @unity
- @nice
- @kornilov

@attack
title: We must attack the bourgeois leadership that got us to this point!
view-if: player_party = "b" or (player_party = "m" and m_leadership = 1) or (player_party = "sr" and sr_leadership = 1)
on-arrival: prov_gov_legitimacy -= 3; m_k_relation -= 5*(1-m_dissent) if player_party = "m"; sr_k_relation -= 5*(1-sr_dissent) if player_party = "sr";

Our representatives at the conference verbally attack the bourgeois representatives.

@unity
title: This is an opportunity to show cross-class unity.
view-if: player_party = "sr" or player_party = "m"
on-arrival: m_k_relation += 5*(1-m_dissent) if player_party = "m"; sr_k_relation += 5*(1-sr_dissent) if player_party = "sr"; defensist_strength += 5; defensist_dissent -= 5; right_sr_strength += 5; right_sr_dissent -= 5; prov_gov_legitimacy += 3; m_b_relation -= 4; b_sr_relation -= 4

We attempt to make nice with the bourgeois figures at the conference. Of course, the Bolsheviks accuse us of class collaborationism.

@nice
title: Perhaps we can reconcile with the socialists...
view-if: player_party = "k"
on-arrival: m_k_relation += 5*(1-k_dissent); sr_k_relation += 5*(1-k_dissent); defensist_strength += 5; defensist_dissent -= 5; right_sr_strength += 5; right_sr_dissent -= 5; prov_gov_legitimacy += 3;

We use the Conference in order to improve our relations with the socialist parties.

@kornilov
title: That General Kornilov is an interesting figure...
subtitle: There are murmurings of military intervention into politics...
unavailable-subtitle: [? if k_conservative_relation < 60: We do not have adequate relations with the conservatives. ?][? if left_kadet_strength >= right_kadet_strength : The right-wing Kadets must be leading in the party. ?]
view-if: player_party = "k"
choose-if: k_conservative_relation >= 60 and right_kadet_strength > left_kadet_strength
on-arrival: coup_progress += 1;

General Lavr Kornilov is one of the most popular and energetic generals in the military, and there are rumors that he wants to take drastic measures to preserve Russia against Bolshevism.

Would we support a military coup to restore order to Petrograd and abolish the Soviet, and keep the country together until the Constituent Assembly?

- @k_support_coup
- @k_no_coup

@k_support_coup
title: Yes, we must do whatever it takes.
on-arrival: coup_progress += 10; k_coup = 4; k_prov_gov_policy = -2;

Negotiations are held in the shadows of the conference. There will be an action soon. Let us hope it is not too late.

@k_no_coup
title: No, this is going too far.
on-arrival: coup_progress -= 1

Despite everything, a coup is going too far.
