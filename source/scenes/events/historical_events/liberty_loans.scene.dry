title: Liberty Loans 
subtitle: The war must be paid for, somehow...
view-if: month = 3 and week = 4 and ((player_party = "m" and not m_in_gov and m_in_exco) or (player_party = "b" and not b_in_gov and b_in_exco) or (player_party = "sr" and not sr_in_gov and sr_in_exco))
new-page: true
on-arrival: liberty_loan_seen += 1;
max-visits: 1
face-image: img/freedom_bonds.jpg
tags: event

= Liberty Loans

The Provisional Government is issuing a new set of war bonds, called the "Liberty Loan", in order to reduce the budget deficit and to reduce inflation by reducing the amount of currency in circulation. They are asking the Soviet to convince the workers to purchase these bonds.

- @support_loan: Support the Liberty Loans.
- @neutral_loan: The Soviet will neither endorse nor reject the loan.
- @oppose_loan: Oppose the Liberty Loan, asking the workers to reject the loan.

@support_loan
on-arrival: budget += 2; monthly_inflation -= 0.3; defensist_strength += 5*(1-m_dissent); right_sr_strength += 5*(1-sr_dissent); internationalist_dissent += 5; left_sr_dissent += 5; soviet_cooperation += 0.05; m_k_relation += 5*(1-m_dissent); sr_k_relation += 5*(1-sr_dissent); bonding_capacity -= 2;
choose-if: player_party != "b"
unavailable-subtitle: The Bolsheviks would never support this.

Through the Soviet Executive Committee, we have supported the loans, and are asking the workers to pay to subscribe to the loan. This relieves the budget situation somewhat, and cuts inflation. In addition, our relations with the Provisional Government and the Kadets has improved. On the other hand, the left wing of the Soviet (the Menshevik-Internationalists and left-SRs) are displeased.

@neutral_loan
on-arrival: budget += 2; monthly_inflation -= 0.3; bonding_capacity -= 2;

We have adopted a position of neutrality with regards to the loan.

@oppose_loan
on-arrival: budget += 1; monthly_inflation -= 0.1; defensist_dissent += 5; right_sr_dissent += 5; left_sr_strength += 5*(1-sr_dissent); internationalist_strength += 5*(1-m_dissent); soviet_cooperation -= 0.05; prov_gov_efficacy -= 0.05; prov_gov_legitimacy -= 5; m_k_relation -= 5; sr_k_relation -= 5; bonding_capacity -= 1;

In opposing the Liberty Loans, we have weakened any possible cooperation with the Provisional Government, and increased the strength of the left-wing factions of the Soviet parties (the Menshevik-Internationalists and left-SRs).
