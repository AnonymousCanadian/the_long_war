title: [+ war_minister +] Offensive
subtitle: Finally, the long-awaited offensive is near...
new-page: true
view-if: month = 6 and week >= 2 and offensive_timer = 0 and ((player_party = "m" and war_minister_party != "Menshevik") or (player_party = "k" and war_minister_party != "Kadet") or (player_party = "sr" and war_minister_party != "SR") or player_party = "b") and not is_armistice and not is_peace
on-arrival: southwestern_front_soldiers += 50; reserve_soldiers -= 50; morale += 0.1; discipline += 0.1; civilian_industry -= 5; military_industry += 5; military_trains += 5; civilian_trains -= 5;
max-visits: 1
tags: event

= [+ war_minister +] Offensive

Finally, the long-anticipated offensive is near. War Minister [+ war_minister +] is planning a major offensive against Germany and Austria on the Southwestern Front.

In preparation for the offensive, additional industries and transportation capacity has been assigned to military usage, and discipline has been restored in the military.

- @stop
- @continue: Let's hope for the best...

@stop
title: Can we stop the offensive somehow?
choose-if: (player_party = "m" and m_war_policy > 1) or (player_party = "sr" and sr_war_policy > 1) or (player_party = "k" and k_war_policy >= 1) or player_party = "b"
unavailable-subtitle: Our party supports the war effort.

Is there anything we can do to stop the offensive?

- @convince
- @protest
- @resign
- @continue: Never mind; there is nothing we can do.

@convince
title: Convince the War Minister to cancel the offensive.
choose-if: (player_party = "m" and ((war_minister_party = "Kadet" and m_k_relation >= 60) or (war_minister_party = "SR" and m_sr_relation >= 75))) or (player_party = "sr" and ((war_minister_party = "Kadet" and sr_k_relation >= 60) or (war_minister_party = "Menshevik" and m_sr_relation >= 75))) or (player_party = "k" and ((war_minister_party = "SR" and sr_k_relation >= 60) or (war_minister_party = "Menshevik" and m_k_relation >= 60)))
unavailable-subtitle: Our relations are not good enough.
on-arrival: civilian_industry += 5; military_industry -= 5; military_trains -= 5; civilian_trains += 5; internationalist_strength += 10; left_sr_strength += 10; left_kadet_strength += 10;

We have successfully convinced War Minister [+ war_minister +] to cancel the planned offensive! The trains and industrial plants must be given back to civilian usage. This is a major victory for the internationalist factions of all parties.

@protest
title: Protest on the streets against the planned offensive.
on-arrival: in_june_offensive = 1; june_offensive_conclusion_timer = 1; offensive_timer += 8; attacking = 1; prov_gov_legitimacy -= 3;

We hold large street demonstrations, but they fail to sway the Provisional Government. The offensive will continue.


@resign
title: Resign from the Provisional Government in protest.
view-if: (player_party = "m" and m_in_prov_gov) or (player_party = "sr" and sr_in_prov_gov)
on-arrival:  in_june_offensive = 1; june_offensive_conclusion_timer = 1; m_in_prov_gov = 0 if player_party = "m"; sr_in_prov_gov = 0 if player_party = "sr"; offensive_timer += 8;
go-to: m_exit_gov if player_party = "m"; sr_exit_gov if player_party = "sr"

We have resigned from the Provisional Government in protest, but nevertheless, the offensive will continue.

@continue
on-arrival: in_june_offensive = 1; june_offensive_conclusion_timer = 1; attacking = 1; offensive_timer += 8;

Everyone hopes that the offensive will be successful.
