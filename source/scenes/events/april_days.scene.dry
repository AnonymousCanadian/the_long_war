title: [+ month : month +] Crisis
subtitle: The people are outraged about the Provisional Government's war policy...
new-page: true
view-if: ((allied_demands_seen = 1 and not allied_demands_rejected) or k_is_player = 0) and april_days_seen = 0 and ((month = 4 and week >= 3) or month > 4)
on-arrival: april_days_seen += 1; b_k_relation -= 10; b_threat += 1; workers_b += 5*(1-b_dissent); soldiers_b += 5*(1-b_dissent);
max-visits: 1
face-image: img/womens_day.jpg
tags: event
go-to: april_days_k if player_party = "k"; april_days_m if player_party = "m"; april_days_sr if player_party = "sr"; april_days_b if player_party = "b"

= [+ month : month +] Crisis

Mass protests in Petrograd! News has spread of Foreign Minister Milyukov's secret letter to the Allies assuring them of the redoubling of our war effort. This has radicalized the workers and the soldiers of the Petrograd garrison, and now they are marching against the imperialist war, and are demanding the ouster of Milyukov.

@april_days_m
go-to: enter_govt if m_k_relation >= 30; tensions if m_k_relation < 30

@april_days_sr
go-to: enter_govt if sr_k_relation >= 30; tensions if sr_k_relation < 30

@enter_govt

The Kadets in the Provisional Government are ready to be rid of Milyukov and allow more socialists to join the government, if it would ease the tensions with the workers and soldiers.

- @join_govt_m: Yes, we will join the Provisional Government!
- @join_govt_sr: Yes, we will join the Provisional Government!
- @no_join_govt_m: No, we will not join the government.
- @no_join_govt_sr: No, we will not join the government.

@join_govt_m
view-if: player_party = "m"
on-arrival: m_influence = m_k_relation/2 + 5; m_in_gov = 1; sr_in_gov = 1; agriculture_minister = "Chernov"; agriculture_minister_party = "SR"; war_minister = "Kerensky"; war_minister_party = "SR"; post_minister = "Tsereteli"; post_minister_party = "Menshevik"; foreign_minister = "Tereshchenko"; m_prov_gov_policy = 0;

# TODO: event to join cabinet

We have accepted the Kadets' offer to join the Provisional Government, as have the SRs. Among the SRs, Viktor Chernov is now Minister of Agriculture, and Alexander Kerensky is now Minister of War. Milyukov has resigned, replaced by Tereshchenko, also Kadet-aligned. The Menshevik leader Irakli Tsereteli has been given the Ministry of Posts and Telegraph, a position created specifically to allow him to join the cabinet.

- @m_join_prov_gov: We should choose our ministries.

@m_demand
title: We will only join if socialists can have a leading role - we must have the position of Prime Minister!
choose-if: m_k_relation >= 60
unavailable-subtitle: They are unwilling to give us the role.
on-arrival: prime_minister = "Tsereteli"; prime_minister_party = "Menshevik"; m_influence = m_k_relation; m_in_gov = 1; sr_in_gov = 1; agriculture_minister = "Chernov"; agriculture_minister_party = "SR"; war_minister = "Kerensky"; war_minister_party = "SR"; post_minister = "Tsereteli"; post_minister_party = "Menshevik"; foreign_minister = "Tereshchenko";

- @m_join_prov_gov: We should choose our ministries.

@join_govt_sr
view-if: player_party = "sr"
on-arrival: sr_influence = sr_k_relation/2 + 5; sr_in_gov = 1; m_in_gov = 1; labor_minister = "Skobelev"; labor_minister_party = "Menshevik"; post_minister = "Tsereteli"; post_minister_party = "Menshevik"; foreign_minister = "Tereshchenko"; sr_prov_gov_policy = 0; left_sr_dissent += 8;

We have accepted the Kadets' offer to join the Provisional Government, as have the Mensheviks. Milyukov has resigned, replaced by Tereshchenko, also Kadet-aligned. The Menshevik leader Irakli Tsereteli has been given the Ministry of Posts and Telegraph.

There is great demand to place Viktor Chernov as Minister of Agriculture, while Alexander Kerensky wants the War Ministry.

- @sr_join_prov_gov: We should choose our ministries.

@no_join_govt_m
view-if: player_party = "m"

Are we sure about this? Is this the best choice for the revolution?

- @join_govt_m: On second thought, we will join the Provisional Government!
- @no_join_m_2: Yes, we are sure.

@no_join_m_2
on-arrival: defensist_dissent += 8; social_patriot_dissent += 8; m_k_relation -= 10; prov_gov_legitimacy -= 5; sr_in_gov = 1; m_sr_relation -= 6; m_b_relation += 6; internationalist_dissent -= 5; internationalist_strength += 5; sr_in_gov = 1; agriculture_minister = "Chernov"; agriculture_minister_party = "SR"; war_minister = "Kerensky"; war_minister_party = "SR"; foreign_minister = "Tereshchenko";

We have decided against joining the Provisional Government. This increases dissent among the defensist factions of our party, hurts our relations with the Kadets, and reduces the legitimacy of the Provisional Government.

The Socialist Revolutionaries, on the other hand, have decided to take a greater part in the Provisional Government. The ambitious Alexander Kerensky is now Minister of War, and Viktor Chernov is Minister of Agriculture.

@no_join_govt_sr
view-if: player_party = "sr"

Are we sure about this? Is this the best choice for the revolution?

- @join_govt_sr: On second thought, we will join the Provisional Government!
- @no_join_sr_2: Yes, we are sure.

@no_join_sr_2
on-arrival: right_sr_dissent += 8; sr_k_relation -= 10; left_sr_strength += 8; left_sr_dissent -= 6; prov_gov_legitimacy -= 5; m_in_gov = 1; post_minister = "Tsereteli"; post_minister_party = "Menshevik"; labor_minister = "Skobelev"; labor_minister_party = "Menshevik";

We have decided against joining the Provisional Government. This increases dissent among the defensist factions of our party, hurts our relations with the Kadets, and reduces the legitimacy of the Provisional Government.

The Mensheviks, on the other hand, have decided to take a greater part in the Provisional Government. Irakli Tsereteli has been appointed Minister of Posts and Telegraphs, and Matvey Skobelev is Minister of Labor.


@tensions
on-arrival: soviet_cooperation *= 0.9; prov_gov_legitimacy -= 10; left_sr_strength += 8; internationalist_strength += 8; labor_unrest += 2; peasant_unrest += 2; soldiers_b += 5; workers_b += 5;

Despite the demands of the streets, the Provisional Government might not budge from its course. The Kadets seem intent on keeping the socialists away from government.

Eventually, the protests die down on their own, but the Provisional Government's legitimacy may have been permanently damaged, as has support for the socialists who supported government cooperation.


@april_days_k

Some in the Kadets are ready to be rid of Milyukov, if it would ease the tension with the socialists. Many in the party are willing to let the moderate socialists (Mensheviks and SRs) join the Provisional Government.

- @let_join: Let the socialists join!
- @refuse: Do not let the socialists join the government.

@let_join
on-arrival: right_kadet_dissent += 8; defensist_strength += 5; right_sr_strength += 5; sr_in_gov = 1; m_in_gov = 1; war_minister = "Kerensky"; war_minister_party = "SR"; labor_minister = "Skobelev"; labor_minister_party = "Menshevik"; agriculture_minister = "Chernov"; agriculture_minister_party = "SR"; post_minister = "Tsereteli"; post_minister_party = "Menshevik"; foreign_minister = "Tereshchenko"; finance_minister = "Shingarev"; finance_minister_party = "Kadet";

The Mensheviks and Socialist-Revolutionaries have joined the Provisional Government! For the SRs, Viktor Chernov is Minister of Agriculture and Alexander Kerensky is Minister of War. For the Mensheviks, Irakli Tsereteli is Minister of Posts and Telegraph and Matvey Skobelev is Minister of Labor. Milyukov has resigned, and the position of Foreign Minister is filled by Mikhail Tereshchenko.

@refuse
on-arrival: left_sr_strength += 15; internationalist_strength += 15; defensis_strength -= 5; labor_unrest += 2; peasant_unrest += 2; prov_gov_legitimacy -= 10; soldiers_b += 5; workers_b += 5;

We have refused to let the socialists join the government.

Eventually, the protests die down on their own, but the Provisional Government's legitimacy may have been permanently damaged, as has support for the socialists who supported government cooperation.


@april_days_b
on-arrival: defensist_strength += 5; right_sr_strength += 5; sr_in_gov = 1; m_in_gov = 1; war_minister = "Kerensky"; war_minister_party = "SR"; labor_minister = "Skobelev"; labor_minister_party = "Menshevik"; agriculture_minister = "Chernov"; agriculture_minister_party = "SR"; post_minister = "Tsereteli"; post_minister_party = "Menshevik"; foreign_minister = "Tereshchenko"; finance_minister = "Shingarev"; finance_minister_party = "Kadet";

In response to the protests, the Kadets have let the bourgeois socialists, the Mensheviks and SRs, join the government. For the SRs, Viktor Chernov is Minister of Agriculture and Alexander Kerensky is Minister of War. For the Mensheviks, Irakli Tsereteli is Minister of Posts and Telegraph and Matvey Skobelev is Minister of Labor. Milyukov has resigned, and the position of Foreign Minister is filled by Mikhail Tereshchenko.

This is an opportunity for the Bolsheviks to recruit and organize among the workers, who might become disillusioned by the failure of the moderate socialists.
